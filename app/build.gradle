apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def buildNumber = Integer.parseInt(System.getenv("BITRISE_BUILD_NUMBER") ?: "1")
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 28
    buildToolsVersion = globalBuildToolsVersion
    defaultConfig {
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        applicationId "tv.letsrobot.controller.android"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode buildNumber
        versionName "0.4.2"
        versionNameSuffix '(' + versionCode + ')-' + getGitHash()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.google.android.material:material:1.0.0'
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    // Required for instrumented tests
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation "androidx.test.ext:junit:1.0.0"
    implementation 'com.google.zxing:core:3.3.3'
    implementation project(':letsrobotandroid-api')
}
repositories {
    google()
    mavenCentral()
}
